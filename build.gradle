apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: "jacoco"
apply plugin: 'java'
apply plugin: 'jdepend'
apply plugin: 'pmd'

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

repositories {
    jcenter()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.12"

    compile 'org.slf4j:slf4j-api:1.7.21'

    compile group: "org.cfg4j", name:"cfg4j-core", version: "4.4.0"
    compile group: 'com.google.guava', name: 'guava', version: '21.0'

    testCompile 'org.testng:testng:6.8.1'
}

test.useTestNG()

checkstyle {
    toolVersion = "7.7"
}

findbugs {
    effort = "max"
    ignoreFailures = false
    reportLevel = "low"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}

jacocoTestCoverageVerification {
    violationRules {
        failOnViolation = true
        rule {
            limit {
                // TODO Increase to 0.8
                minimum = 0.6
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

jdepend {
    ignoreFailures = false
}

pmd {
    ignoreFailures = false
    rulePriority = 5
}